	list	on
	include	a:bios.def
	include	a:fs.def

; boot code
	org	reset
	di
	ld	sp,stack
	jp	init

; rst vectors are used for fast virtual method calls
	org	virt0
	ld	l,(ix+0)
	ld	h,(ix+1)
	jp	(hl)

	org	virt1
	ld	l,(ix+2)
	ld	h,(ix+3)
	jp	(hl)

	org	virt2
	ld	l,(ix+4)
	ld	h,(ix+5)
	jp	(hl)

	org	virt3
	ld	l,(ix+6)
	ld	h,(ix+7)
	jp	(hl)

	org	virt4
	ld	l,(ix+8)
	ld	h,(ix+9)
	jp	(hl)

	org	virt5
	ld	l,(ix+10)
	ld	h,(ix+11)
	jp	(hl)

; rst 38h (opcode ff) -> error handler
	org	error
	di
	push	ix
	call	errprnt
	db	' IX:',0
	call	errtrac
	call	errprnt
	db	'  STACK:',0
	ld	b,8
errdbg	call	errtrac
	djnz	errdbg
errhang	halt
	jr	errhang

; non-maskable interrupt
	org	nmi
	rst	error

; title string near the beginning for easier rom identification
title	db	27,'[2J',27,'[H',27,'[0m'
	db	12,'Loop Micro BIOS - Bootstrap Stage 1',13,10
titleln	equ	$-title

; print function for error handler (direct i/o)
errprnt	pop	hl
errprn0	ld	a,(hl)
	inc	hl
	and	a,a
	jr	nz,errprn1
	jp	(hl)
errprn1	call	errout
	jr	errprn0

; dump word from stack (direct i/o)
errtrac	ld	a,20h
	call	errout
	pop	hl
	ex	(sp),hl
	ld	a,h
	call	errhex
	ld	a,l
errhex	ld	e,a
	rrca
	rrca
	rrca
	rrca
	call	errnib
	ld	a,e
errnib	and	a,0fh
	add	a,90h
	daa
	adc	a,40h
	daa
errout	out	(0bch),a
	push	af
errout1	in	a,(0c3h)
	and	a,04h
	jr	z,errout1
	pop	af
	out	(0c2h),a
	ret

; more virtual method calls
	org	virt6
	ld	l,(ix+12)
	ld	h,(ix+13)
	jp	(hl)
	org	virt7
	ld	l,(ix+14)
	ld	h,(ix+15)
	jp	(hl)
	org	virt8
	ld	l,(ix+16)
	ld	h,(ix+17)
	jp	(hl)

	include a:utils.inc
	include	a:device.inc
	include	a:video.inc
	include	a:keyboard.inc
	include	a:console.inc
	include	a:serial.inc
	include	a:multi.inc
	include	a:rootfs.inc
	include	a:memdev.inc
	include	a:system.inc

; main entry point
; clear stack memory
init	ld	hl,stackl
	ld	de,stackl+1
	ld	bc,timekpr-stackl-1
	ld	(hl),0ffh
	ldir
; initialize hardware
	call	sysinit
; welcome message
	ld	ix,(dvlist+term)
	ld	de,title
	ld	bc,titleln
	rst	dvwrite

	call	shell
last	rst	error

	org	shell
