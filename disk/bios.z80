charmap	equ	5000h;..6fffh	character bitmap
stack	equ	7f00h;		stack below this address
dvspace	equ	7f00h;..7fefh	device data space
temp	equ	7ff0h;..7ff7h	scratch space
timekpr	equ	7ff8h;..7fffh	real time clock / calendar

user	equ	8000h ; user code area

	list	on

; boot code
	org	0
	di
	ld	sp,stack
	jp	init

; rst vectors are used for fast virtual method calls
	org	08h
virt0	ld	l,(ix+0)
	ld	h,(ix+1)
	jp	(hl)

	org	10h
virt1	ld	l,(ix+2)
	ld	h,(ix+3)
	jp	(hl)

	org	18h
virt2	ld	l,(ix+4)
	ld	h,(ix+5)
	jp	(hl)

	org	20h
virt3	ld	l,(ix+6)
	ld	h,(ix+7)
	jp	(hl)

	org	28h
virt4	ld	l,(ix+8)
	ld	h,(ix+9)
	jp	(hl)

	org	30h
virt5	ld	l,(ix+10)
	ld	h,(ix+11)
	jp	(hl)

; rst 38h (opcode ff) -> error handler
	org	38h
error	di
	halt
	jr	error

; more virtual method calls
virt6	ld	l,(ix+12)
	ld	h,(ix+13)
	jp	(hl)
virt7	ld	l,(ix+14)
	ld	h,(ix+15)
	jp	(hl)
virt8	ld	l,(ix+16)
	ld	h,(ix+17)
	jp	(hl)

; non-maskable interrupt
	org	66h
	rst	error

	org	80h

; title string near the beginning for easier rom identification
title	db	'Loop Micro - Bootstrap Stage 1',13,10
titleln	equ	$-title

	include	a:device.inc
	include	a:video.inc
	include	a:keyboard.inc
	include	a:console.inc
	include	a:system.inc

; main entry point
; clear free memory
init	ld	hl,last
	ld	de,last+1
	ld	bc,charmap-last-1
	ld	(hl),0ffh
	ldir
	ld	hl,charmap+2000h
	ld	de,charmap+2000h+1
	ld	bc,timekpr-[charmap+2000h]-1
	ld	(hl),0ffh
	ldir
	ld	hl,user
	ld	de,user+1
	ld	bc,7fffh
	ld	(hl),0ffh
	ldir
; initialize video
	ld	ix,video
	ld	b,0b0h
	call	dvinit
; initialize keyboard
	ld	ix,keybrd
	ld	b,0c0h
	call	dvinit
; initialize console
	ld	ix,console
	ld	de,keybrd
	ld	bc,video
	call	dvinit

term	equ	console

; welcome message
	ld	ix,term
	ld	de,title
	ld	bc,titleln
	rst	dvwrite

	ld	ix,term
test	rst	dvgetc
	rst	dvputc
	jr	test

;	call	user
last	rst	error

