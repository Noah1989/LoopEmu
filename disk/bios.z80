charmap equ	5000h ;.. 6fffh character bitmap
temp	equ	7ff0h ;.. 7ff7h scratch space
timekpr	equ	7ff8h ;.. 7fffh real time clock / calendar

; video I/O
vscrx	equ	0b0h ; scroll x (lower byte)
vscry	equ	0b1h ; scroll y (lower byte)
vscrh	equ	0b2h ; scroll x/y high bits, modifiers
vadrl	equ	0b3h ; video address (lower byte)
vadrh	equ	0b4h ; video address (upper byte)
vname	equ	0b8h ; name table (select pattern for each tile)
vattr	equ	0b9h ; attribute table (select palette for each tile)
vpatt	equ	0bah ; pattern table (256 8x8 patterns, 32 bytes each)
vpale	equ	0bbh ; palette table (512 palettes, 16 8-bit colors each)
vincr	equ	4    ; add this to I/O port no. for address auto increment

; boot code
	org	0
	di
	ld	sp,temp
	jp	init
; rst vectors
	org	08h
	call	error
	org	10h
	call	error
	org	18h
	call	error
	org	20h
	call	error
	org	28h
	call	error
	org	30h
	call	error
; rst 38h (opcode ff) -> error handler
	org	38h
error	di
	halt
	jr	error

	org	40h
initmsg	db	'Loop Micro - Bootstrap Stage 1', 0

; non-maskable interrupt
	org	66h
	rst	38h ; error

	org	80h
; clear free memory
init	ld	hl,last
	ld	de,last+1
	ld	bc,charmap-last-1
	ldir
	ld	hl,8000h
	ld	de,8001h
	ld	bc,7fffh
	ld	(hl),0ffh
	ldir
; set video parameters
	ld	a,-3
	out	(vscrx),a
	ld	a,-4
	out	(vscry),a
	ld	a,10110011b
	out	(vscrh),a
; load character map
	xor	a,a
	out	(vadrl),a
	out	(vadrh),a
	ld	ix,charmap
	ld	c, 0
init0	exx	; alternate
	ld	b,4
init1	ld	d,(ix+2)
	ld	e,(ix+3)
	ld	h,(ix+18)
	ld	l,(ix+19)
	exx	; normal
	ld	d,(ix+0)
	ld	e,(ix+1)
	ld	h,(ix+16)
	ld	l,(ix+17)
	ld	b,8
init2	ld	a,80h ; falls into carry after 8 rotations
init21	rl	d
	rra
	rl	e
	rra
	rl	h
	rra
	rl	l
	rra
	exx	; always executed twice
	jr	nc,init21
	out	(vpatt+vincr),a
	djnz	init2
	ld	de,4
	add	ix,de
	exx	; alternate
	djnz	init1
	exx	; normal
	ld	de,16
	add	ix,de
	inc	c
	jr	nz,init0
; load palettes
	ld	hl, plight
	call	loadpal
; clear name and attribute tables
	ld	bc,2000h
init3	ld	a,67h
	out	(vattr),a
	ld	a,' '
	out	(vname+vincr),a
	dec	bc
	ld	a,b
	or	a,c
	jr	nz,init3
; welcome message
	xor	a
	out	(vadrl),a
	out	(vadrh),a
	ld	c,vname+vincr
	ld	hl,initmsg
	call	outstr
	rst	38h ; error

; load palettes from 24 colors pointed to by hl:
; 8 background colors
; 8 regular forground colors
; 8 high contrast foreground colors
; intended for use with a 2-font 8x16 character map
loadpal	xor	a,a
	ld	d, h
	ld	e, l
	ld	hl,8
	add	hl,de
	out	(vadrl),a
	out	(vadrh),a
	ld	ix,temp
	ld	(ix+0),8 ; pattern bit count
	ld	(ix+1),1 ; pattern bit width
	ld	(ix+2),4 ; palette sets
loadp0	ld	(ix+3),8 ; background colors
loadp1	ld	(ix+4),16; foreground colors
; output a single palette
loadp2	ld	c,(ix+0)
loadp3	ld	a,(de)
	ld	b,(ix+1)
loadp4	out	(vpale+vincr),a
	djnz	loadp4
	ld	a,(hl)
	ld	b,(ix+1)
loadp5	out	(vpale+vincr),a
	djnz	loadp5
	dec	c
	jr	nz,loadp3
; next foreground color
	inc	hl
	dec	(ix+4)
	jr	nz,loadp2
; next background color
	ld	bc,-16
	add	hl,bc
	inc	de
	dec	(ix+3)
	jr	nz,loadp1
; next palette set
	ex	de,hl
	ld	bc,-8
	add	hl,bc
	ex	de,hl
	srl	(ix+0)
	sla	(ix+1)
	dec	(ix+2)
	jr	nz,loadp0
	ret
pdark	db	000h,005h,011h,015h,041h,045h,051h,054h
	db	002h,019h,020h,029h,081h,098h,0e0h,0fdh
	db	0a8h,01ah,022h,02ah,092h,0ddh,0e2h,0feh
plight	db	0ffh,0afh,0bbh,0bfh,0ebh,0efh,0fbh,0abh
	db	0fch,00eh,023h,02bh,0c2h,0deh,0e3h,003h
	db	057h,00dh,021h,028h,0c1h,0dch,0e1h,001h

; output null terminated string at (hl) to device at port (c)
; returns with hl pointing to the first byte after the string
outstr	ld	a,(hl)
	inc	hl
	and	a,a
	ret	z
	out	(c),a
	jr	outstr

last	rst	38h ; error
