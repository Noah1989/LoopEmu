	list	on

	include	a:bios.def
	include	a:system.def
	include	a:device.def
	include	a:utils.def

	org	shell
	jp	shmain

shmaxln	equ	80	; maximum input line length

; shell data block (128 bytes)
shlnbfl	equ	shdata		; length of data in line buffer
shlnbuf	equ	shlnbfl+1	; line buffer space
shlnbnt	equ	shlnbuf+shmaxln ; reserved for null terminator of full buffer

shinit	xor	a,a
	ld	(shlnbfl),a
	ld	(shlnbnt),a
	ret

shbannr	db 27,'[1m'
	db 27,'[93m __                ',27,'[94m_____ _',13,10
	db 27,'[93m|  |   ___ ___ ___',27,'[94m|     |_|___ ___ ___ ',13,10
	db 27,'[93m|  |__| . | . | . |',27,'[94m | | | |  _|  _| . |',13,10
	db 27,'[93m|_____|___|___|  _|',27,'[94m_|_|_|_|___|_| |___|',13,10
	db	27,'[22;92mshell running ',27,'[1;93m|_|',27,'[22;92m ver. '
	include	a:version.def
	db	13,10
	db 	27,'[0m',13,10
shbanrl	equ	$-shbannr

shprmpt	db	27,'[1;92m>',27,'[m '
shprmtl	equ	$-shprmpt

shmain	ld	ix,(dvlist+term)
	ld	de,shbannr
	ld	bc,shbanrl
	rst	dvwrite
shloop	ld	de,shprmpt
	ld	bc,shprmtl
	rst	dvwrite
	call	shinit
	call	shrdln
	ld	a,13
	rst	dvputc
	ld	a,10
	rst	dvputc
	ld	a,(shlnbfl)
	ld	c,a
	ld	b,0
	ld	de,shlnbuf
	rst	dvwrite
	ld	a,13
	rst	dvputc
	ld	a,10
	rst	dvputc
	jr	shloop

shrdln	ld	ix,(dvlist+term)
shrdln0	rst	dvgetc
	cp	a,20h
	jr	c,shrdln2
	cp	a,7fh
	jr	nc,shrdln2
; normal character, add to buffer and echo
shrdln1	ld	e,a ; save character for later
	ld	a,(shlnbfl)
	cp	a,shmaxln
	jr	nc,shrdln0 ; buffer full
	ld	hl,shlnbuf
	ld	c,a
	ld	b,0
	add	hl,bc
	ld	(hl),e
	inc	hl
	ld	(hl),0
	inc	a
	ld	(shlnbfl),a
	ld	a,e
	rst	dvputc
	jr	shrdln0
; handle control character
shrdln2	call	switch
	db	8	; backspace
	dw	shrdlbs
	db	13	; return
	dw	shrdlcr
	db	7fh	; delete
	dw	shrdlbs
	db	0	; default
	dw	shrdln0

; handle backspace
shrdlbs	ld	a,(shlnbfl)
	and	a,a
	jr	z,shrdln0
	dec	a
	ld	(shlnbfl),a
	ld	a,8
	rst	dvputc
	ld	a,' '
	rst	dvputc
	ld	a,8
	rst	dvputc
	jr	shrdln0

; handle carriage return (enter)
shrdlcr	ret

